// Tactics Remix, Morpheus562's Improved Gnoll Stronghold and Tougher Gnolls 

///////////////////////////////////////////////////////////////////////////
// Import                                                                //
///////////////////////////////////////////////////////////////////////////
ACTION_IF NOT FILE_EXISTS_IN_GAME ~mohal02.itm~ THEN BEGIN
  COPY ~tactics-remix/itm/MOHAL02.ITM~ ~override/MOHAL02.ITM~
    SAY NAME1 @50100
    SAY NAME2 @50102
    SAY UNIDENTIFIED_DESC @50101
    SAY DESC @50103
    WRITE_ASCII 0x58 chalb06
    WRITE_LONG 0x34 3000
END

///////////////////////////////////////////////////////////////////////////
// Improve Gnolls                                                        //
///////////////////////////////////////////////////////////////////////////

///////////////////////////////////////////////////////////////////////////
// Update Gnoll                                                          //
// Level: 2                                                              //
// HP: 20                                                                //
// Thac0: 19                                                             //
// STR: 14                                                               //
// DEX: 12                                                               //
// CON: 11                                                               //
// INT: 6                                                                //
// WIS: 10                                                               //
// CHA: 7                                                                //
// Leave scripts alone, SCS handles well enough                          //
//                                                                       //
///////////////////////////////////////////////////////////////////////////
ACTION_FOR_EACH gnoll IN
      aegnoll  // windspear hills
      dagnoll  // Ust Natha
      gnoll    // generic gnoll bgee
      gnoll01  // not found in game (bg2ee)
      gnoll02  // not found in game, +1 halberd
      gnoll03  // Mgnl.2da
      gnoll04  // Mgnl.2da
      gnoll05  // not found in game, +1 halberd
      gnoll2   // not found in game
      gnoll3   // not found in game
      gnoll5a  // Found with Ludrug. Need to update class script
      gnoll_b  // generic gnoll
      gnoll_bb // bandits, special override
      gnolld   // drizzt gnolls, special race
      gnollper // perdue sword
      gnoltal2 // bandit camp (cave)
      gnlwar01 // spawngrp.2da
    BEGIN
      ACTION_IF FILE_EXISTS_IN_GAME ~%gnoll%.CRE~ BEGIN
        
        COPY_EXISTING ~%gnoll%.CRE~ ~override~
          WRITE_BYTE 0x234  2  // Level
          WRITE_BYTE 0x24  20  // Current HP
          WRITE_BYTE 0x26  20  // Maximum HP
          WRITE_BYTE 0x52  19  // thac0
          WRITE_BYTE 0x53  1   // APR
          WRITE_BYTE 0x238 14  // Strength
          WRITE_BYTE 0x23a  6  // Intelligence
          WRITE_BYTE 0x23b 10  // Wisdom
          WRITE_BYTE 0x23c 12  // Dexterity
          WRITE_BYTE 0x23d 11  // Constitution
          WRITE_BYTE 0x23e  7  // Charisma
          SET_BG2_PROFICIENCY ~PROFICIENCY2HANDED~ 2
          SET_BG2_PROFICIENCY ~PROFICIENCYHALBERD~ 2
      END
    END

///////////////////////////////////////////////////////////////////////////
// Update Gnoll Slasher                                                  //
// Level: 3                                                              //
// HP: 30                                                                //
// Thac0: 18                                                             //
// STR: 16                                                               //
// DEX: 14                                                               //
// CON: 16                                                               //
// INT: 10                                                               //
// WIS: 10                                                               //
// CHA: 9                                                                //
// Leave scripts alone, SCS handles well enough                          //
//                                                                       //
///////////////////////////////////////////////////////////////////////////
ACTION_FOR_EACH slasher IN
      bdshem03 // SoD, Tomb Safehouse
      gnoll_d  // bgee generic
      gnoltal3 // bandit camp
      gnlsla01 // spawngrp.2da
    BEGIN
      ACTION_IF FILE_EXISTS_IN_GAME ~%slasher%.CRE~ BEGIN
        
        COPY_EXISTING ~%slasher%.CRE~ ~override~
          WRITE_BYTE 0x234  3  // Level
          WRITE_BYTE 0x24  30  // Current HP
          WRITE_BYTE 0x26  30  // Maximum HP
          WRITE_BYTE 0x52  18  // thac0
          WRITE_BYTE 0x53  1   // APR
          WRITE_BYTE 0x238 16  // Strength
          WRITE_BYTE 0x23a 10  // Intelligence
          WRITE_BYTE 0x23b 10  // Wisdom
          WRITE_BYTE 0x23c 14  // Dexterity
          WRITE_BYTE 0x23d 16  // Constitution
          WRITE_BYTE 0x23e  9  // Charisma
          SET_BG2_PROFICIENCY ~PROFICIENCY2HANDED~ 2
          SET_BG2_PROFICIENCY ~PROFICIENCYHALBERD~ 3
      END
    END

///////////////////////////////////////////////////////////////////////////
// Update Gnoll Veteran                                                  //
// Level: 4                                                              //
// HP: 40                                                                //
// Thac0: 17                                                             //
// STR: 17                                                               //
// DEX: 14                                                               //
// CON: 16                                                               //
// INT: 10                                                               //
// WIS: 10                                                               //
// CHA: 9                                                                //
// Leave scripts alone, SCS handles well enough                          //
//                                                                       //
///////////////////////////////////////////////////////////////////////////
ACTION_FOR_EACH veteran IN
      gnoll_e  // bgee generic
      gnoll_eb // bandit camp
      gnoltal4 // bandit camp, cave
      gnoll5   // Ludrug
      gnlvet01 // spawngrp.2da
    BEGIN
      ACTION_IF FILE_EXISTS_IN_GAME ~%veteran%.CRE~ BEGIN
        
        COPY_EXISTING ~%veteran%.CRE~ ~override~
          WRITE_BYTE 0x234  3  // Level
          WRITE_BYTE 0x24  30  // Current HP
          WRITE_BYTE 0x26  30  // Maximum HP
          WRITE_BYTE 0x52  18  // thac0
          WRITE_BYTE 0x53  1   // APR
          WRITE_BYTE 0x238 17  // Strength
          WRITE_BYTE 0x23a 10  // Intelligence
          WRITE_BYTE 0x23b 10  // Wisdom
          WRITE_BYTE 0x23c 14  // Dexterity
          WRITE_BYTE 0x23d 16  // Constitution
          WRITE_BYTE 0x23e  9  // Charisma
          SET_BG2_PROFICIENCY ~PROFICIENCY2HANDED~ 2
          SET_BG2_PROFICIENCY ~PROFICIENCYHALBERD~ 3
      END
    END

///////////////////////////////////////////////////////////////////////////
// Update Gnoll Elite                                                    //
// Level: 5                                                              //
// HP: 50                                                                //
// Thac0: 16                                                             //
// STR: 18/5                                                             //
// DEX: 14                                                               //
// CON: 16                                                               //
// INT: 10                                                               //
// WIS: 10                                                               //
// CHA: 9                                                                //
// Leave scripts alone, SCS handles well enough                          //
//                                                                       //
///////////////////////////////////////////////////////////////////////////
ACTION_FOR_EACH elite IN
      gnoll_a  // bgee generic
      gnollsu  // summoned
      gnoltal1 // bandit camp
      gnleli01 // spawngrp.2da
      ohngnel1 // neera wild forest
      ohngnel2 // neera wild forest
      //palkni03 // He is found in BG2EE Windspear and will not include here to keep special
    BEGIN
      ACTION_IF FILE_EXISTS_IN_GAME ~%elite%.CRE~ BEGIN
        
        COPY_EXISTING ~%elite%.CRE~ ~override~
          WRITE_BYTE 0x234  5  // Level
          WRITE_BYTE 0x24  50  // Current HP
          WRITE_BYTE 0x26  50  // Maximum HP
          WRITE_BYTE 0x52  16  // thac0
          WRITE_BYTE 0x53  1   // APR
          WRITE_BYTE 0x238 18  // Strength
          WRITE_BYTE 0x239  5  // Strength bonus
          WRITE_BYTE 0x23a 10  // Intelligence
          WRITE_BYTE 0x23b 10  // Wisdom
          WRITE_BYTE 0x23c 14  // Dexterity
          WRITE_BYTE 0x23d 16  // Constitution
          WRITE_BYTE 0x23e  9  // Charisma
          SET_BG2_PROFICIENCY ~PROFICIENCY2HANDED~ 2
          SET_BG2_PROFICIENCY ~PROFICIENCYHALBERD~ 3
      END
    END

///////////////////////////////////////////////////////////////////////////
// Update Flind                                                          //
// Level: 5                                                              //
// HP: 50                                                                //
// Thac0: 16                                                             //
// STR: 18/25                                                            //
// DEX: 14                                                               //
// CON: 16                                                               //
// INT: 10                                                               //
// WIS: 10                                                               //
// CHA: 9                                                                //
// Leave scripts alone, SCS handles well enough                          //
//                                                                       //
///////////////////////////////////////////////////////////////////////////
ACTION_FOR_EACH flind IN
      flind    // bgee generic
      flind_a  // not in game
      flind_b  // not in game
      flind_c  // not in game
      flindb   // bandit camp
      flintal1 // bandit camp and peldvale
      plyflind // shapeshift
      gnlfli01 // not in game
    BEGIN
      ACTION_IF FILE_EXISTS_IN_GAME ~%flind%.CRE~ BEGIN
        
        COPY_EXISTING ~%flind%.CRE~ ~override~
          WRITE_BYTE 0x234  5  // Level
          WRITE_BYTE 0x24  50  // Current HP
          WRITE_BYTE 0x26  50  // Maximum HP
          WRITE_BYTE 0x52  16  // thac0
          WRITE_BYTE 0x53  1   // APR
          WRITE_BYTE 0x238 18  // Strength
          WRITE_BYTE 0x239 25  // Strength bonus
          WRITE_BYTE 0x23a 10  // Intelligence
          WRITE_BYTE 0x23b 10  // Wisdom
          WRITE_BYTE 0x23c 14  // Dexterity
          WRITE_BYTE 0x23d 16  // Constitution
          WRITE_BYTE 0x23e  9  // Charisma
          SET_BG2_PROFICIENCY ~PROFICIENCYSINGLEWEAPON~ 2
          SET_BG2_PROFICIENCY ~PROFICIENCYLONGSWORD~ 3
      END
    END

// Update Flind weapon
ACTION_IF FILE_EXISTS_IN_GAME ~flind1.itm~ BEGIN
  COPY_EXISTING ~flind1.itm~ ~override~
    LPF ALTER_ITEM_HEADER INT_VAR header_type=1 dicesize=10 dicenumber=1 damage_type=3 END
END

///////////////////////////////////////////////////////////////////////////
// Update Gnoll Chieftain                                                //
// Level: 6                                                              //
// HP: 60                                                                //
// Thac0: 15                                                             //
// STR: 18/60                                                            //
// DEX: 16                                                               //
// CON: 18                                                               //
// INT: 10                                                               //
// WIS: 10                                                               //
// CHA: 9                                                                //
// Leave scripts alone, SCS handles well enough                          //
//                                                                       //
///////////////////////////////////////////////////////////////////////////
ACTION_FOR_EACH chieftain IN
      gnoll_c  // bgee generic
    BEGIN
      ACTION_IF FILE_EXISTS_IN_GAME ~%chieftain%.CRE~ BEGIN
        
        COPY_EXISTING ~%chieftain%.CRE~ ~override~
          WRITE_BYTE 0x234  6  // Level
          WRITE_BYTE 0x24  60  // Current HP
          WRITE_BYTE 0x26  60  // Maximum HP
          WRITE_BYTE 0x52  15  // thac0
          WRITE_BYTE 0x53  1   // APR
          WRITE_BYTE 0x238 18  // Strength
          WRITE_BYTE 0x239 60  // Strength bonus
          WRITE_BYTE 0x23a 10  // Intelligence
          WRITE_BYTE 0x23b 10  // Wisdom
          WRITE_BYTE 0x23c 16  // Dexterity
          WRITE_BYTE 0x23d 18  // Constitution
          WRITE_BYTE 0x23e  9  // Charisma
          SET_BG2_PROFICIENCY ~PROFICIENCY2HANDED~ 2
          SET_BG2_PROFICIENCY ~PROFICIENCYHALBERD~ 4
      END
    END

COPY_EXISTING ~gnoll_c.cre~ ~override/mognlc.cre~
  REPLACE_CRE_ITEM ~mohal02~ #0 #0 #0 ~UNSTEALABLE~ ~INV1~ // Will of Atar +1

///////////////////////////////////////////////////////////////////////////
// Create Gnoll Shaman                                                   //
// Level: 4                                                              //
// HP: 32                                                                //
// Thac0: 18                                                             //
// STR: 11                                                               //
// DEX: 12                                                               //
// CON: 12                                                               //
// INT: 9                                                                //
// WIS: 14                                                               //
// CHA: 8                                                                //
//                                                                       //
///////////////////////////////////////////////////////////////////////////
COPY ~tactics-remix/cre/MOGNLS.CRE~ ~override/MOGNLS.CRE~
  SAY NAME1 @2043
  SAY NAME2 @2043

///////////////////////////////////////////////////////////////////////////
// Copy .BCS file to override folder                                     //
///////////////////////////////////////////////////////////////////////////
ACTION_IF GAME_IS ~bgee~ BEGIN
  EXTEND_BOTTOM ~AR5100.bcs~ ~tactics-remix/baf/gnoll/ar5100.baf~
END ELSE ACTION_IF GAME_IS ~EET~ BEGIN
  EXTEND_BOTTOM ~BG5100.bcs~ ~tactics-remix/baf/gnoll/bg5100.baf~
END

COPY ~tactics-remix/baf/gnoll/mognls.baf~ ~override/mognls.bcs~
  COMPILE_BAF_TO_BCS