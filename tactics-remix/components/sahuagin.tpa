// Tactics Remix, Improved Sahuagin City

///////////////////////////////////////////////////////////////////////////
// Create Items                                                          //
///////////////////////////////////////////////////////////////////////////
// Spear +2, +1d4 Cold
COPY_EXISTING ~sper05.itm~ ~override/mosah01.itm~
  LPF ADD_ITEM_EFFECT INT_VAR opcode = 12 type = 1 target = 2 parameter1 = 0 parameter2 = 131072 timing = 1 resist_dispel = 0 probability1 = 100 dicenumber = 1 dicesize = 4 special = 1 END

// Spear +3, Imprisonment on Hit (25% chance and save vs. death at -8)
COPY_EXISTING ~sper06.itm~ ~override/mosah02.itm~
  LPF ADD_ITEM_EFFECT INT_VAR opcode = 25 type = 1 target = 2 parameter1 = 3 parameter2 = 3 timing = 0 resist_dispel = 2 duration = 30 probability1 = 100 END
  LPF ADD_ITEM_EFFECT INT_VAR opcode = 142 type = 1 target = 2 parameter2 = 137 timing = 0 resist_dispel = 2 duration = 30 probability1 = 100 END
  LPF ADD_ITEM_EFFECT INT_VAR opcode = 211 type = 1 target = 2 timing = 1 resist_dispel = 0 duration = 0 probability1 = 25 savingthrow = 4 savebonus = ~-8~ END
  LPF ADD_ITEM_EFFECT INT_VAR opcode = 139 type = 1 target = 2 parameter1 = RESOLVE_STR_REF (@4604) timing = 1 resist_dispel = 0 duration = 0 probability1 = 25 savingthrow = 4 savebonus = ~-8~ END 

///////////////////////////////////////////////////////////////////////////
// Improve Sahuagin                                                      //
///////////////////////////////////////////////////////////////////////////
COPY_EXISTING_REGEXP GLOB ~^.+\.cre$~ ~override~
SPRINT source ~%SOURCE_FILE%~
READ_STRREF 0x8   name
READ_BYTE   0x234 level
READ_BYTE   0x24  current_hp
READ_BYTE   0x272 race
READ_BYTE   0x52  thaco

// Patch if Race == Sahuagin 
PATCH_IF (race = 131) AND !("%source%" STRING_EQUAL_CASE "WILDZONE.CRE") BEGIN
  
  // Do calculations
  level = level + 2
  current_hp = current_hp + 20
  PATCH_IF (thaco < 2) BEGIN
    thaco = 0
  END ELSE BEGIN
    thaco = thaco - 2
  END

  // Update .cre file
  WRITE_BYTE 0x234 level      // Level
  WRITE_BYTE 0x24  current_hp // Current HP
  WRITE_BYTE 0x26  current_hp // Maximum HP
  WRITE_BYTE 0x52  thaco      // thac0
  REPLACE_CRE_ITEM ~OHCHAN50~ #0 #0 #0 ~UNSTEALABLE&UNDROPPABLE~ ~ARMOR~ EQUIP // Splint Mail +2

  // Print out results
  PATCH_PRINT ~%name% (%source%) updated stats: Level(%level%) HP (%current_hp%) thac0 (%thaco%).~
END
BUT_ONLY

///////////////////////////////////////////////////////////////////////////
// Update Priestess Encounter                                            //
///////////////////////////////////////////////////////////////////////////
COPY_EXISTING ~SAHRAMB3.CRE~ ~override~

  // Rearrange scripts if SCS is NOT present
  PATCH_IF !scs_ai BEGIN
    WRITE_ASCII 0x250 ~~         #8 // Clear Class Script
    WRITE_ASCII 0x260 ~PRIES14T~ #8 // Clear General Script
  END
  WRITE_ASCII 0x258 ~MOSAH01~    #8 // Add Race Script

COPY_EXISTING ~SAHANGU.CRE~ ~override~
  REPLACE_CRE_ITEM ~mosah01~   #0 #0 #0 ~UNSTEALABLE&UNDROPPABLE~ ~WEAPON1~ EQUIP

COPY_EXISTING ~SAHANGU.CRE~ ~override/mosaha01.cre~
  WRITE_BYTE 0x270 255 // set allegiance to ENEMY

COPY_EXISTING ~SAHAMB05.CRE~ ~override/mosaha.cre~
  WRITE_BYTE 0x234 20  // Level
  WRITE_LONG 0x14 26000 // XP Value
  WRITE_BYTE 0x24 155  // Current HP
  WRITE_BYTE 0x26 155  // Maximum HP
  WRITE_LONG 0x28 IDS_OF_SYMBOL (animate SALAMANDER_FIRE) // Animation
  WRITE_SHORT 0x46 ~-6~ // Natural AC
  WRITE_SHORT 0x48 ~-6~ // Effective AC
  WRITE_BYTE 0x52   5  // thac0
  WRITE_BYTE 0x53   4  // APR
  WRITE_BYTE 0x54 5 // Save vs. death
  WRITE_BYTE 0x55 7 // Save vs. wand
  WRITE_BYTE 0x56 6 // Save vs. polymorph
  WRITE_BYTE 0x57 5 // Save vs. breath
  WRITE_BYTE 0x58 8 // Save vs. spell
  WRITE_BYTE 0x59 100 // Fire resistance
  WRITE_BYTE 0x5a 10 // Cold resistance
  WRITE_BYTE 0x5b 10 // Electricity resistance
  WRITE_BYTE 0x5c 10 // Acid resistance
  WRITE_BYTE 0x5d 90 // Magic resistance
  WRITE_BYTE 0x5e 10 // Magic fire resistance
  WRITE_BYTE 0x5f 10 // Magic cold resistance
  WRITE_BYTE 0x60 40 // Slashing resistance
  WRITE_BYTE 0x61 40 // Crushing resistance
  WRITE_BYTE 0x62 40 // Piercing resistance
  WRITE_BYTE 0x63 40 // Missile resistance
  WRITE_BYTE 0x238 25 // Strength
  WRITE_BYTE 0x23a 20 // Intelligence
  WRITE_BYTE 0x23b 16 // Wisdom
  WRITE_BYTE 0x23c 19 // Dexterity
  WRITE_BYTE 0x23d  9 // Constitution
  WRITE_BYTE 0x23e 25 // Charisma
  WRITE_BYTE 0x23f 12 // Morale
  WRITE_BYTE 0x241 1 // Racial enemy (Human)
  WRITE_BYTE 0x271 255 // General (Monster)
  WRITE_BYTE 0x272 121 // Race (Demonic)
  WRITE_BYTE 0x273 160 // Class (Tanari)
  WRITE_ASCII 0x248 ~~        #8 // Clear Override Script
  WRITE_ASCII 0x268 ~mosah02~ #8 // Add Default Script
  WRITE_ASCII 0x280 ~mosah02~ #8 // Script Name
  WRITE_BYTE 0x27b 51 // Alignment (Chaotic Evil)
  REPLACE_CRE_ITEM ~IMMUNE3~  #0 #0 #0 ~UNSTEALABLE&UNDROPPABLE~ ~LRING~   EQUIP // Demon immunities
  REPLACE_CRE_ITEM ~RINGDEMN~ #0 #0 #0 ~UNSTEALABLE&UNDROPPABLE~ ~RRING~   EQUIP // More demon immunities
  REPLACE_CRE_ITEM ~mosah02~  #0 #0 #0 ~UNSTEALABLE&UNDROPPABLE~ ~WEAPON1~ EQUIP // New spear
  REPLACE_CRE_ITEM ~SCRL9Z~   #0 #0 #0 ~UNSTEALABLE&UNDROPPABLE~ ~INV1~    EQUIP // Scroll of freedom
  LPF ADD_CRE_EFFECT INT_VAR opcode = 193 target = 0 parameter2 = 1 timing = 1 duration = 0 probability1 = 100 END
  LPF ADD_CRE_EFFECT INT_VAR opcode = 51 target = 0 timing = 9 probability1 = 100 END
  LPF ADD_CRE_EFFECT INT_VAR opcode = 189 target = 0 parameter1 = 4 parameter2 = 0 timing = 1 probability1 = 100 END
  LPF ADD_CRE_EFFECT INT_VAR opcode = 190 target = 0 parameter1 = 4 timing = 1 probability1 = 100 END
  LPF ADD_CRE_EFFECT INT_VAR opcode = 126 target = 0 parameter1 = 8 parameter2 = 0 timing = 1 probability1 = 100 END
  LPF DELETE_EFFECT INT_VAR match_opcode = 233 match_parameter2 = 111 silent = 1 END
  LPF ADD_CRE_EFFECT INT_VAR opcode = 233 parameter1 = 2 parameter2 = 111 timing = 9 END // 2 handed weapon prof  
  PATCH_IF sa_prof BEGIN
    LPF DELETE_EFFECT INT_VAR match_opcode = 233 match_parameter2 = 105 silent = 1 END
    LPF ADD_CRE_EFFECT INT_VAR opcode = 233 parameter1 = 5 parameter2 = 105 timing = 9 END // GM in Polearms
  END ELSE BEGIN
    LPF DELETE_EFFECT INT_VAR match_opcode = 233 match_parameter2 = 98 silent = 1  END
    LPF ADD_CRE_EFFECT INT_VAR opcode = 233 parameter1 = 5 parameter2 = 98 timing = 9 END  // GM in Spears
  END

///////////////////////////////////////////////////////////////////////////
// Copy .BCS file to override folder                                     //
///////////////////////////////////////////////////////////////////////////
COPY ~tactics-remix/baf/sahuagin/mosah01.baf~ ~override/mosah01.bcs~
     ~tactics-remix/baf/sahuagin/mosah02.baf~ ~override/mosah02.bcs~
  COMPILE_BAF_TO_BCS