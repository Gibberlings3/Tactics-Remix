// Ritual Kensai/Thief

/////////////////////////////////////////////////////////////////////
// Prep==0      - Diurnal Rhythms
/////////////////////////////////////////////////////////////////////
IF
    Global("Prep","LOCALS",0)
THEN
  RESPONSE #100
    SetGlobal("Prep","LOCALS",1)
    SetGlobalTimer("PrepTimer","LOCALS",0)
    SetGlobalTimer("KaiTimer","LOCALS",0)
END

/////////////////////////////////////////////////////////////////////
// PrepTimer==0 - Short Spell Protections 
/////////////////////////////////////////////////////////////////////
IF
    GlobalTimerExpired("PrepTimer","LOCALS")
    Allegiance(Myself,ENEMY)
THEN
  RESPONSE #100
        SetGlobalTimer("PrepTimer","LOCALS",3600)
        SetGlobalTimer("Cast","LOCALS",0)
        GiveItemCreate("potn10",Myself,5,0,0)   // invis
        GiveItemCreate("potn52",Myself,2,0,0)   // extra healing
        Continue()
END

/////////////////////////////////////////////////////////////////////
// Hide!
/////////////////////////////////////////////////////////////////////
IF
    !StateCheck(Myself,STATE_INVISIBLE)
    !Detect(NearestEnemyOf(Myself))
THEN
  RESPONSE #100
    Hide()
END

IF
    !StateCheck(Myself,STATE_INVISIBLE)
    HasItem("potn10",Myself)
    GlobalTimerExpired("Cast","LOCALS")
    Allegiance(Myself,ENEMY)
THEN
  RESPONSE #100
    SetGlobalTimer("Cast","LOCALS",6)
    UseItem("potn10",Myself)
END

/////////////////////////////////////////////////////////////////////
// Find a Target
/////////////////////////////////////////////////////////////////////

// Target!
IF
    OR(8) // default ...
      See(Player1)
      See(Player2)
      See(Player3)
      See(Player4)
      See(Player5)
      See(Player6)
      See(WorstAC)
      See(LastAttackerOf(Myself))
    False()
THEN
    RESPONSE #100
END

// Sixth Nearest.
IF
    CheckStat(SixthNearestEnemyOf(Myself),0,STONESKINS)
    !StateCheck(SixthNearestEnemyOf(Myself),STATE_IMPROVEDINVISIBILITY)
    CheckStat(SixthNearestEnemyOf(Myself),0,SANCTUARY)
    See(SixthNearestEnemyOf(Myself))
    False()
THEN
    RESPONSE #100
END
// Fifth Nearest.
IF
    CheckStat(FifthNearestEnemyOf(Myself),0,STONESKINS)
    !StateCheck(FifthNearestEnemyOf(Myself),STATE_IMPROVEDINVISIBILITY)
    CheckStat(FifthNearestEnemyOf(Myself),0,SANCTUARY)
    See(FifthNearestEnemyOf(Myself))
    False()
THEN
    RESPONSE #100
END
// Fourth Nearest.
IF
    CheckStat(FourthNearestEnemyOf(Myself),0,STONESKINS)
    !StateCheck(FourthNearestEnemyOf(Myself),STATE_IMPROVEDINVISIBILITY)
    CheckStat(FourthNearestEnemyOf(Myself),0,SANCTUARY)
    See(FourthNearestEnemyOf(Myself))
    False()
THEN
    RESPONSE #100
END
// Third Nearest.
IF
    CheckStat(ThirdNearestEnemyOf(Myself),0,STONESKINS)
    !StateCheck(ThirdNearestEnemyOf(Myself),STATE_IMPROVEDINVISIBILITY)
    CheckStat(ThirdNearestEnemyOf(Myself),0,SANCTUARY)
    See(ThirdNearestEnemyOf(Myself))
    False()
THEN
    RESPONSE #100
END
// Second Nearest.
IF
    CheckStat(SecondNearestEnemyOf(Myself),0,STONESKINS)
    !StateCheck(SecondNearestEnemyOf(Myself),STATE_IMPROVEDINVISIBILITY)
    CheckStat(SecondNearestEnemyOf(Myself),0,SANCTUARY)
    See(SecondNearestEnemyOf(Myself))
    False()
THEN
    RESPONSE #100
END
//  Nearest.
IF
    CheckStat(NearestEnemyOf(Myself),0,STONESKINS)
    !StateCheck(NearestEnemyOf(Myself),STATE_IMPROVEDINVISIBILITY)
    CheckStat(NearestEnemyOf(Myself),0,SANCTUARY)
    See(NearestEnemyOf(Myself))
    False()
THEN
    RESPONSE #100
END
//  WorstAC.
IF
    CheckStat(WorstAC,0,STONESKINS)
    !StateCheck(WorstAC,STATE_IMPROVEDINVISIBILITY)
    CheckStat(WorstAC,0,SANCTUARY)
    See(WorstAC)
    False()
THEN
    RESPONSE #100
END

/////////////////////////////////////////////////////////////////////
// Heal, Minor
/////////////////////////////////////////////////////////////////////
IF
  GlobalTimerExpired("Cast","LOCALS")
  HasItem("potn52",Myself)
  HPPercentLT(Myself,90)
THEN
  RESPONSE #100
    SetGlobalTimer("Cast","LOCALS",6)
    UseItem("potn52",Myself)
END

/////////////////////////////////////////////////////////////////////
// Invisible Kia Backstab
/////////////////////////////////////////////////////////////////////
IF
  Allegiance(Myself,ENEMY)
  StateCheck(Myself,STATE_INVISIBLE)
  GlobalTimerExpired("Cast","LOCALS")
  GlobalTimerExpired("KaiTimer","LOCALS")
  HaveSpell(KENSAI_KIA)
  Range([PC],10)
THEN
  RESPONSE #100
    Spell(Myself,KENSAI_KIA)
    SetGlobalTimer("KaiTimer","LOCALS",6)
    SetGlobalTimer("Cast","LOCALS",6)
END

IF
  Allegiance(Myself,ENEMY)
  StateCheck(Myself,STATE_INVISIBLE)
  !GlobalTimerExpired("Cast","LOCALS")
  GlobalTimerExpired("KaiTimer","LOCALS")
  HaveSpell(KENSAI_KIA)
THEN
  RESPONSE #100
    MoveToObject(Player1) // move in to position
END

IF
  Allegiance(Myself,ENEMY)
  StateCheck(Myself,STATE_INVISIBLE)
  !GlobalTimerExpired("KaiTimer","LOCALS")
THEN
  RESPONSE #100
    DisplayStringHead(Myself,25511) // Intruders!
    Shout(125)
    Attack(LastSeenBy(Myself))
END

/////////////////////////////////////////////////////////////////////
// Normal Attack
/////////////////////////////////////////////////////////////////////
IF
  Allegiance(Myself,ENEMY)
  OR(2)
    !StateCheck(Myself,STATE_INVISIBLE)
    !HaveSpell(KENSAI_KIA)
THEN
  RESPONSE #100
    DisplayStringHead(Myself,25511) // Intruders!
    Shout(125)
    Attack(LastSeenBy(Myself))
END

/////////////////////////////////////////////////////////////////////
// Detect Illusions
/////////////////////////////////////////////////////////////////////
IF
  StateCheck(Myself,STATE_INVISIBLE)
  !ModalState(DETECTTRAPS)
THEN
  RESPONSE #100
    FindTraps()
END

/////////////////////////////////////////////////////////////////////
// Find them!
/////////////////////////////////////////////////////////////////////
IF
  Heard([ENEMY],125)
  !Detect([GOODCUTOFF])
THEN
  RESPONSE #100
    MoveToObject(LastHeardBy(Myself))
END

IF
  Allegiance(Myself,ENEMY)
  StateCheck(Myself,STATE_INVISIBLE)
  !Detect([GOODCUTOFF])
THEN
  RESPONSE #100
    RandomWalkContinuous()
END
