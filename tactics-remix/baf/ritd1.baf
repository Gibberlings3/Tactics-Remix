/////////////////////////////////////////////////////////////////////
// Ritual Totemic Druid
/////////////////////////////////////////////////////////////////////
// 
// Spells:
// CLW x4
// Bless x2
// Armor x2
// Charm x7
// Light x6
// CWB x4
// Iron Skins x1
// Insect Plague x1
// Physical Mirror x1
// Fire Elemental
// Spirit Animal x3

/////////////////////////////////////////////////////////////////////
// Prep==0      - Diurnal Rhythms
/////////////////////////////////////////////////////////////////////
IF
    Global("Prep","LOCALS",0)
THEN
  RESPONSE #100
    SetGlobal("Prep","LOCALS",1)
    SetGlobalTimer("PrepTimer","LOCALS",0)
END

/////////////////////////////////////////////////////////////////////
// PrepTimer==0 - Short Spell Protections 
/////////////////////////////////////////////////////////////////////
IF
    GlobalTimerExpired("PrepTimer","LOCALS")
    Allegiance(Myself,ENEMY)
THEN
  RESPONSE #100
        SetGlobalTimer("PrepTimer","LOCALS",3600)
        SetGlobalTimer("Cast","LOCALS",0)
        GiveItemCreate("potn52",Myself,2,0,0)   // extra healing
        Continue()
END

/////////////////////////////////////////////////////////////////////
// Spell: Stoneskin (if out)
/////////////////////////////////////////////////////////////////////
IF
	GlobalTimerExpired("Cast","LOCALS")
        HaveSpell(CLERIC_IRONSKIN)
	CheckStatLT(Myself,1,STONESKINS)
        GlobalTimerExpired("Cast","LOCALS")
THEN
	RESPONSE #100
          SetGlobalTimer("Cast","LOCALS",6)
          Spell(Myself,CLERIC_IRONSKIN)
END

/////////////////////////////////////////////////////////////////////
// Find a Target
/////////////////////////////////////////////////////////////////////
IF
    OR(8) // default ...
      See(Player1)
      See(Player2)
      See(Player3)
      See(Player4)
      See(Player5)
      See(Player6)
      See(WorstAC)
      See(LastAttackerOf(Myself))
    False()
THEN
    RESPONSE #100
END

IF
  CheckStatLT(NearestEnemyOfType([GOODCUTOFF]),30,RESISTMAGIC)
  !Allegiance(NearestEnemyOfType([GOODCUTOFF]),ENEMY)
  See(NearestEnemyOfType([GOODCUTOFF]))
  False()
THEN 
  RESPONSE #100
END

IF
  CheckStatLT(NearestEnemyOfType([0.0.0.BARD_ALL]),30,RESISTMAGIC)
  !Allegiance(NearestEnemyOfType([0.0.0.BARD_ALL]),ENEMY)
  See(NearestEnemyOfType([0.0.0.BARD_ALL]))
  False()
THEN 
  RESPONSE #100
END

IF
  CheckStatLT(NearestEnemyOfType([0.0.0.CLERIC_ALL]),30,RESISTMAGIC)
  !Allegiance(NearestEnemyOfType([0.0.0.CLERIC_ALL]),ENEMY)
  See(NearestEnemyOfType([0.0.0.CLERIC_ALL]))
  False()
THEN 
  RESPONSE #100
END

IF
  CheckStatLT(NearestEnemyOfType([0.0.0.DRUID_ALL]),30,RESISTMAGIC)
  !Allegiance(NearestEnemyOfType([0.0.0.DRUID_ALL]),ENEMY)
  See(NearestEnemyOfType([0.0.0.DRUID_ALL]))
  False()
THEN 
  RESPONSE #100
END

IF
  CheckStatLT(NearestEnemyOfType([0.0.0.MAGE_ALL]),30,RESISTMAGIC)
  !Allegiance(NearestEnemyOfType([0.0.0.MAGE_ALL]),ENEMY)
  See(NearestEnemyOfType([0.0.0.MAGE_ALL]))
  False()
THEN 
  RESPONSE #100
END

/////////////////////////////////////////////////////////////////////
// Spells
/////////////////////////////////////////////////////////////////////
IF 
  HaveSpell(CLERIC_INSECT_PLAGUE)
  GlobalTimerExpired("Cast","LOCALS")
  !Gender(LastSeenBy(Myself),SUMMONED)
  CheckStatLT(LastSeenBy(Myself),30,RESISTMAGIC)
  !Allegiance(LastSeenBy(Myself),ENEMY)
THEN
  RESPONSE #100
    SetGlobalTimer("Cast","LOCALS",6)
    Spell(LastSeenBy(Myself),CLERIC_INSECT_PLAGUE)
END

IF 
  HaveSpell(CLERIC_CONJURE_FIRE_ELEMENTAL)
  GlobalTimerExpired("Cast","LOCALS")
  !CombatCounter(0)
  Range([GOODCUTOFF],100)
THEN
  RESPONSE #100
    SetGlobalTimer("Cast","LOCALS",6)
    Spell(LastSeenBy(Myself),CLERIC_CONJURE_FIRE_ELEMENTAL)
END

IF
  HaveSpell(CLERIC_PHYSICAL_MIRROR)
  GlobalTimerExpired("Cast","LOCALS")
  Exists(LastAttackerOf(Myself))
  !Range(LastAttackerOf(Myself),3)
  !CombatCounter(0)
THEN
  RESPONSE #100
    SetGlobalTimer("Cast","LOCALS",6)
    Spell(Myself,CLERIC_PHYSICAL_MIRROR)
END

IF 
  HaveSpell(CLERIC_CALL_LIGHTNING)
  GlobalTimerExpired("Cast","LOCALS")
  !Gender(LastSeenBy(Myself),SUMMONED)
  CheckStatLT(LastSeenBy(Myself),30,RESISTELECTRICITY)
  CheckStatLT(LastSeenBy(Myself),30,RESISTMAGIC)
  !Allegiance(LastSeenBy(Myself),ENEMY)
THEN
  RESPONSE #100
    SetGlobalTimer("Cast","LOCALS",6)
    Spell(LastSeenBy(Myself),CLERIC_CALL_LIGHTNING)
END

/////////////////////////////////////////////////////////////////////
// Heal, Major
/////////////////////////////////////////////////////////////////////
IF
  GlobalTimerExpired("Cast","LOCALS")
  HasItem("potn52",Myself)
  HPPercentLT(Myself,50)
THEN
  RESPONSE #100
    SetGlobalTimer("Cast","LOCALS",6)
    UseItem("potn52",Myself)
END

IF 
  HaveSpell(CLERIC_CALL_WOODLAND_BEINGS)
  GlobalTimerExpired("Cast","LOCALS")
  !CombatCounter(0)
  Range([GOODCUTOFF],100)
THEN
  RESPONSE #100
    SetGlobalTimer("Cast","LOCALS",6)
    Spell(Myself,CLERIC_CALL_WOODLAND_BEINGS)
END

IF 
  HaveSpell(CLERIC_HOLD_PERSON)
  GlobalTimerExpired("Cast","LOCALS")
  CheckStatLT(LastSeenBy(Myself),30,RESISTMAGIC)
  CheckStatGT(LastSeenBy(Myself),1,SAVEVSSPELL)
  !Allegiance(LastSeenBy(Myself),ENEMY)
  !Kit(LastSeenBy(Myself),INQUISITOR)
  !HasItemEquiped("FREERING",LastSeenBy(Myself)) // Ring of Free Action
  !HasItemEquiped("ROSSRING",LastSeenBy(Myself)) // Ring of Free Action
  !HasItemEquiped("RING09",LastSeenBy(Myself)) // Ring of Free Action
  !HasItemEquiped("NPSW01",LastSeenBy(Myself)) // Sword of Avoreen
  !StateCheck(LastSeenBy(Myself),STATE_HELPLESS)
   OR(6) // only slow fighter-types
    Class(LastSeenBy(Myself),RANGER_ALL)
    Class(LastSeenBy(Myself),FIGHTER_ALL)
    Class(LastSeenBy(Myself),PALADIN)
    Class(LastSeenBy(Myself),THIEF)
    Kit(LastSeenBy(Myself),BARBARIAN)
    Gender(LastSeenBy(Myself),SUMMONED)
THEN
  RESPONSE #100
    SetGlobalTimer("Cast","LOCALS",6)
    Spell(LastSeenBy(Myself),CLERIC_HOLD_PERSON)
END

IF      
    GlobalTimerExpired("Cast","LOCALS")
    HaveSpell(CLERIC_CHARM_PERSON)
    CheckStatGT(LastSeenBy(Myself),1,SAVEVSSPELL)
    !StateCheck(LastSeenBy(Myself),STATE_BERSERK) // Minsc Berserk
    CheckStat(LastSeenBy(Myself),0,SCRIPTINGSTATE3) // Barbarian Rage
    !Class(LastSeenBy(Myself),MONK)
        !Kit(LastSeenBy(Myself),INQUISITOR)
        !HasItemEquiped("HELM06",LastSeenBy(Myself)) // Helm of Charm Protection
        !HasItemEquiped("MISCBC",LastSeenBy(Myself)) // Blackrazor
        !HasItemEquiped("NPCHAN",LastSeenBy(Myself)) // Corthala Family Armor
        !HasItemEquiped("REAVER",LastSeenBy(Myself)) // Unholy Reaver
        !HasItemEquiped("SHLD25",LastSeenBy(Myself)) // Shield of Harmony +2
        !HasItemEquipedReal("SPER07",LastSeenBy(Myself)) // Spear of the Unicorn +2           
        !HasItemEquipedReal("STAF11",LastSeenBy(Myself)) // Staff of the Magi
        !HasItemEquipedReal("SW1H35",LastSeenBy(Myself)) // Adjatha the Drinker +2
        !HasItemEquipedReal("SW1H54",LastSeenBy(Myself)) // The Equalizer
        !HasItemEquipedReal("SW2H14",LastSeenBy(Myself)) // Lilarcor
    //!StateCheck(LastSeenBy(Myself),STATE_OUT_OF_ACTION)
    !Allegiance(LastSeenBy(Myself),ENEMY)
THEN
  RESPONSE #100
    SetGlobalTimer("Cast","LOCALS",6)
    Spell(LastSeenBy(Myself),CLERIC_CHARM_PERSON)
END

/////////////////////////////////////////////////////////////////////
// Helping Spells
/////////////////////////////////////////////////////////////////////

IF
  HaveSpell(CLERIC_CHANT)
  GlobalTimerExpired("Cast","LOCALS")
  OR(4)
    See([ENEMY.0.0.FIGHTER_ALL])
    See([ENEMY.0.0.RANGER_ALL])
    See([ENEMY.0.0.PALADIN_ALL])
    See([ENEMY.0.0.0.0.SUMMONED])
  !StateCheck(LastSeenBy(Myself),STATE_CHANT)
  !CombatCounter(0)
  Range([GOODCUTOFF],100)
THEN
  RESPONSE #100
    SetGlobalTimer("Cast","LOCALS",6)
    Spell(LastSeenBy(Myself),CLERIC_CHANT)
END

IF
  HaveSpell(CLERIC_BLESS)
  GlobalTimerExpired("Cast","LOCALS")
  OR(4)
    See([ENEMY.0.0.FIGHTER_ALL])
    See([ENEMY.0.0.RANGER_ALL])
    See([ENEMY.0.0.PALADIN_ALL])
    See([ENEMY.0.0.0.0.SUMMONED])
  !StateCheck(LastSeenBy(Myself),STATE_BLESS)
  !CombatCounter(0)
  Range([GOODCUTOFF],100)
THEN
  RESPONSE #100
    SetGlobalTimer("Cast","LOCALS",6)
    Spell(LastSeenBy(Myself),CLERIC_BLESS)
END

IF
  HaveSpell(CLERIC_DRAW_UPON_HOLY_MIGHT)
  GlobalTimerExpired("Cast","LOCALS")
  !StateCheck(Myself,STATE_DRAWUPONHOLYMIGHT)
  !CombatCounter(0)
  Range([GOODCUTOFF],100)
THEN
  RESPONSE #100
    SetGlobalTimer("Cast","LOCALS",6)
    Spell(Myself,CLERIC_DRAW_UPON_HOLY_MIGHT)
END

/////////////////////////////////////////////////////////////////////
// Heal, Minor
/////////////////////////////////////////////////////////////////////
IF
  GlobalTimerExpired("Cast","LOCALS")
  HasItem("potn52",Myself)
  HPPercentLT(Myself,90)
THEN
  RESPONSE #100
    SetGlobalTimer("Cast","LOCALS",6)
    UseItem("potn52",Myself)
END

/////////////////////////////////////////////////////////////////////
// Normal Attack
/////////////////////////////////////////////////////////////////////
IF
  Allegiance(Myself,ENEMY)
  !GlobalTimerExpired("Cast","LOCALS")
  See(WorstAC)
THEN
  RESPONSE #100
    Attack(LastSeenBy(Myself))
END

IF
  Allegiance(Myself,ENEMY)
  !GlobalTimerExpired("Cast","LOCALS")
  See(NearestEnemyOfType([PC]))
THEN
  RESPONSE #100
    Attack(LastSeenBy(Myself))
END

/////////////////////////////////////////////////////////////////////
// Find them!
/////////////////////////////////////////////////////////////////////
IF
  Heard([ENEMY],125)
  !Detect([GOODCUTOFF])
THEN
  RESPONSE #100
    MoveToObject(LastHeardBy(Myself))
END
