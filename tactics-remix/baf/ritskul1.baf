/////////////////////////////////////////////////////////////////////
// Cannot be killed until the guardians are destroyed!
/////////////////////////////////////////////////////////////////////
IF
  HasItem("mel01",Myself)
  !Detect([ENEMY])
THEN
  RESPONSE #100
    DisplayString(Myself,61791) // nooo!
    DestroyItem("mel01")        // no more "can't be killed"
    DestroyItem("ringwolf")     // no more regeneration, which was
                                // actually just gravy
    Continue()
END

IF 
  HP(Myself,1)
  HasItem("mel01",Myself)
  Delay(10)
THEN
  RESPONSE #100
    DisplayString(Myself,54680)
    Continue()
END

/////////////////////////////////////////////////////////////////////
// Report: call for help!
/////////////////////////////////////////////////////////////////////
IF
  See([GOODCUTOFF])
  !Allegiance(LastSeenBy(Myself),ENEMY)
  Delay(12)
THEN
  RESPONSE #100
    Shout(125)
    Continue()
END

/////////////////////////////////////////////////////////////////////
// Avoid Death by [Offscreen] Cloud
/////////////////////////////////////////////////////////////////////
IF
  Delay(3)
  OR(6)
    HitBy([GOODCUTOFF],POISON) // wand of cloudkill
    SpellCast([GOODCUTOFF],WIZARD_CLOUDKILL)
    SpellCast([GOODCUTOFF],WIZARD_STINKING_CLOUD)
    SpellCast([GOODCUTOFF],WIZARD_INCENDIARY_CLOUD)
    SpellCast([GOODCUTOFF],CLERIC_STORM_OF_VENGEANCE)
    SpellCast([GOODCUTOFF],WIZARD_DEATH_FOG)
THEN
  RESPONSE #100
    ForceSpell(Myself,CLERIC_ZONE_OF_SWEET_AIR)
END

/////////////////////////////////////////////////////////////////////
// Main Magic Power
/////////////////////////////////////////////////////////////////////
IF
  Allegiance(Myself,ENEMY)
  See([GOODCUTOFF])
  Gender(LastSeenBy(Myself),SUMMONED)
  !Allegiance(LastSeenBy(Myself),ENEMY)
  Delay(5)
THEN
  RESPONSE #100
    ReallyForceSpellRES("ritskul1",LastSeenBy(Myself))
END

IF
  Allegiance(Myself,ENEMY)
  See([GOODCUTOFF])
  !Allegiance(LastSeenBy(Myself),ENEMY)
  Delay(12)
THEN
  RESPONSE #100
    ReallyForceSpellRES("ritskul1",LastSeenBy(Myself))
END
