/////////////////////////////////////////////////////////////////////
/////////////////////////////////////////////////////////////////////
//            Improved Smarter Guarded Compound Scripts
/////////////////////////////////////////////////////////////////////
// 				Version 1
// Maferan, 17th level Fighter
// Olaf Rasmusen, 17th level Fighter
//
// Fixes: These guys are now grandmasters with their respective
// weapons (axes and two-handers). 
//
// Improvements: They won't go downstairs. Their target selection
// is improved to favor mages and the weak. You can still lead them
// on a merry chase around the room (these guys aren't that smart)
// ... and you're welcome to do so, if you've managed to take everyone
// else out. :-)
/////////////////////////////////////////////////////////////////////

/////////////////////////////////////////////////////////////////////
// Battle Prep
// 
// The GC crew knows you are invading, so they prepare by casting
// a few long-lived spells while waiting for the demon and efreeti 
// to take you out. 
/////////////////////////////////////////////////////////////////////
IF
  Detect(NearestEnemyOf(Myself))
  Global("Prep","LOCALS",0)
THEN
  RESPONSE #100
    SetGlobal("Prep","LOCALS",1)
    SetGlobalTimer("Cast","LOCALS",0)
    GiveItemCreate("potn52",Myself,5,0,0)   // extra healing
    Continue()
END

IF 
  Global("MO_DifficultyCheck","LOCALS",0)
  DifficultyLT(HARDEST)
THEN
  RESPONSE #100
    DestroyItem("BELT11")
    DestroyItem("BRAC10")
    DestroyItem("RING07")
    SetGlobal("MO_DifficultyCheck","LOCALS",1)
END

/////////////////////////////////////////////////////////////////////
// Rage
/////////////////////////////////////////////////////////////////////
IF
  !GlobalTimerNotExpired("MO_SpellCastTimer","LOCALS")
  See([GOODCUTOFF])
  Allegiance(Myself,ENEMY)
  HaveSpell(BARBARIAN_RAGE)  // SPCL152.SPL (Barbarian Rage)
  !CheckSpellState(Myself,BARBARIAN_RAGE)
THEN
  RESPONSE #100
    SetGlobalTimer("MO_SpellCastTimer","LOCALS",6)
    SetInterrupt(FALSE)
    Spell(Myself,BARBARIAN_RAGE)
    SetInterrupt(TRUE)
END

IF
  !GlobalTimerNotExpired("MO_SpellCastTimer","LOCALS")
  See([GOODCUTOFF])
  Allegiance(Myself,ENEMY)
  HaveSpell(BERSERKER_RAGE)  // SPCL321.SPL (Enrage)
  !CheckSpellState(Myself,BERSERKER_RAGE_FATIGUE)
  !CheckSpellState(Myself,BERSERKER_RAGE)
THEN
  RESPONSE #100
    SetGlobalTimer("MO_SpellCastTimer","LOCALS",6)
    SetInterrupt(FALSE)
    Spell(Myself,BERSERKER_RAGE)  // SPCL321.SPL (Enrage)
    SetInterrupt(TRUE)
END

/////////////////////////////////////////////////////////////////////
// Heal!
/////////////////////////////////////////////////////////////////////
IF
  !GlobalTimerNotExpired("MO_SpellCastTimer","LOCALS")
  HasItem("potn52",Myself)
  HPPercentLT(Myself,50)
THEN
  RESPONSE #100
    SetGlobalTimer("MO_SpellCastTimer","LOCALS",6)
    SetInterrupt(FALSE)
    UseItem("potn52",Myself)
    SetInterrupt(TRUE)
END

/////////////////////////////////////////////////////////////////////
// Attack
/////////////////////////////////////////////////////////////////////
IF
  ActionListEmpty()
  See(NearestEnemyOf(Myself))
  !Allegiance(LastSeenBy(Myself),ENEMY)
  CheckStatLT(LastSeenBy(Myself),1,STONESKINS)
  CheckStatGT(LastSeenBy(Myself),-20,ARMORCLASS)
  CheckStatLT(LastSeenBy(Myself),60,RESISTSLASHING)
  CheckStatLT(LastSeenBy(Myself),60,RESISTCRUSHING)
  CheckStatLT(LastSeenBy(Myself),60,RESISTPIERCING)
  CheckStatLT(LastSeenBy(Myself),60,RESISTMISSILE)
  !StateCheck(LastSeenBy(Myself),STATE_NOT_APPROACHABLE)
  WeaponEffectiveVs(NearestEnemyOf(Myself),MAINHAND)
  WeaponCanDamage(NearestEnemyOf(Myself),MAINHAND)
THEN
  RESPONSE #100
    AttackOneRound(NearestEnemyOf(Myself))
END

IF
  ActionListEmpty()
  See(SecondNearestEnemyOf(Myself))
  !Allegiance(LastSeenBy(Myself),ENEMY)
  CheckStatLT(LastSeenBy(Myself),1,STONESKINS)
  CheckStatGT(LastSeenBy(Myself),-20,ARMORCLASS)
  CheckStatLT(LastSeenBy(Myself),60,RESISTSLASHING)
  CheckStatLT(LastSeenBy(Myself),60,RESISTCRUSHING)
  CheckStatLT(LastSeenBy(Myself),60,RESISTPIERCING)
  CheckStatLT(LastSeenBy(Myself),60,RESISTMISSILE)
  !StateCheck(LastSeenBy(Myself),STATE_NOT_APPROACHABLE)
  WeaponEffectiveVs(SecondNearestEnemyOf(Myself),MAINHAND)
  WeaponCanDamage(SecondNearestEnemyOf(Myself),MAINHAND)
THEN
  RESPONSE #100
    AttackOneRound(SecondNearestEnemyOf(Myself))
END

IF
  ActionListEmpty()
  See(ThirdNearestEnemyOf(Myself))
  !Allegiance(LastSeenBy(Myself),ENEMY)
  CheckStatLT(LastSeenBy(Myself),1,STONESKINS)
  CheckStatGT(LastSeenBy(Myself),-20,ARMORCLASS)
  CheckStatLT(LastSeenBy(Myself),60,RESISTSLASHING)
  CheckStatLT(LastSeenBy(Myself),60,RESISTCRUSHING)
  CheckStatLT(LastSeenBy(Myself),60,RESISTPIERCING)
  CheckStatLT(LastSeenBy(Myself),60,RESISTMISSILE)
  !StateCheck(LastSeenBy(Myself),STATE_NOT_APPROACHABLE)
  WeaponEffectiveVs(ThirdNearestEnemyOf(Myself),MAINHAND)
  WeaponCanDamage(ThirdNearestEnemyOf(Myself),MAINHAND)
THEN
  RESPONSE #100
    AttackOneRound(ThirdNearestEnemyOf(Myself))
END

IF
  ActionListEmpty()
  See(FourthNearestEnemyOf(Myself))
  !Allegiance(LastSeenBy(Myself),ENEMY)
  CheckStatLT(LastSeenBy(Myself),1,STONESKINS)
  CheckStatGT(LastSeenBy(Myself),-20,ARMORCLASS)
  CheckStatLT(LastSeenBy(Myself),60,RESISTSLASHING)
  CheckStatLT(LastSeenBy(Myself),60,RESISTCRUSHING)
  CheckStatLT(LastSeenBy(Myself),60,RESISTPIERCING)
  CheckStatLT(LastSeenBy(Myself),60,RESISTMISSILE)
  !StateCheck(LastSeenBy(Myself),STATE_NOT_APPROACHABLE)
  WeaponEffectiveVs(FourthNearestEnemyOf(Myself),MAINHAND)
  WeaponCanDamage(FourthNearestEnemyOf(Myself),MAINHAND)
THEN
  RESPONSE #100
    AttackOneRound(FourthNearestEnemyOf(Myself))
END

IF
  ActionListEmpty()
  See(FifthNearestEnemyOf(Myself))
  !Allegiance(LastSeenBy(Myself),ENEMY)
  CheckStatLT(LastSeenBy(Myself),1,STONESKINS)
  CheckStatGT(LastSeenBy(Myself),-20,ARMORCLASS)
  CheckStatLT(LastSeenBy(Myself),60,RESISTSLASHING)
  CheckStatLT(LastSeenBy(Myself),60,RESISTCRUSHING)
  CheckStatLT(LastSeenBy(Myself),60,RESISTPIERCING)
  CheckStatLT(LastSeenBy(Myself),60,RESISTMISSILE)
  !StateCheck(LastSeenBy(Myself),STATE_NOT_APPROACHABLE)
  WeaponEffectiveVs(FifthNearestEnemyOf(Myself),MAINHAND)
  WeaponCanDamage(FifthNearestEnemyOf(Myself),MAINHAND)
THEN
  RESPONSE #100
    AttackOneRound(FifthNearestEnemyOf(Myself))
END

IF
  ActionListEmpty()
  See(SixthNearestEnemyOf(Myself))
  !Allegiance(LastSeenBy(Myself),ENEMY)
  CheckStatLT(LastSeenBy(Myself),1,STONESKINS)
  CheckStatGT(LastSeenBy(Myself),-20,ARMORCLASS)
  CheckStatLT(LastSeenBy(Myself),60,RESISTSLASHING)
  CheckStatLT(LastSeenBy(Myself),60,RESISTCRUSHING)
  CheckStatLT(LastSeenBy(Myself),60,RESISTPIERCING)
  CheckStatLT(LastSeenBy(Myself),60,RESISTMISSILE)
  !StateCheck(LastSeenBy(Myself),STATE_NOT_APPROACHABLE)
  WeaponEffectiveVs(SixthNearestEnemyOf(Myself),MAINHAND)
  WeaponCanDamage(SixthNearestEnemyOf(Myself),MAINHAND)
THEN
  RESPONSE #100
    AttackOneRound(SixthNearestEnemyOf(Myself))
END

IF
  ActionListEmpty()
  See(NearestEnemyOf(Myself))
  !Allegiance(LastSeenBy(Myself),ENEMY)
  !StateCheck(LastSeenBy(Myself),STATE_NOT_APPROACHABLE)
  WeaponEffectiveVs(NearestEnemyOf(Myself),MAINHAND)
  WeaponCanDamage(NearestEnemyOf(Myself),MAINHAND)
THEN
  RESPONSE #100
    AttackOneRound(NearestEnemyOf(Myself))
END

IF
  ActionListEmpty()
  See(SecondNearestEnemyOf(Myself))
  !Allegiance(LastSeenBy(Myself),ENEMY)
  !StateCheck(LastSeenBy(Myself),STATE_NOT_APPROACHABLE)
  WeaponEffectiveVs(SecondNearestEnemyOf(Myself),MAINHAND)
  WeaponCanDamage(SecondNearestEnemyOf(Myself),MAINHAND)
THEN
  RESPONSE #100
    AttackOneRound(SecondNearestEnemyOf(Myself))
END

IF
  ActionListEmpty()
  See(ThirdNearestEnemyOf(Myself))
  !Allegiance(LastSeenBy(Myself),ENEMY)
  !StateCheck(LastSeenBy(Myself),STATE_NOT_APPROACHABLE)
  WeaponEffectiveVs(ThirdNearestEnemyOf(Myself),MAINHAND)
  WeaponCanDamage(ThirdNearestEnemyOf(Myself),MAINHAND)
THEN
  RESPONSE #100
    AttackOneRound(ThirdNearestEnemyOf(Myself))
END

IF
  ActionListEmpty()
  See(FourthNearestEnemyOf(Myself))
  !Allegiance(LastSeenBy(Myself),ENEMY)
  !StateCheck(LastSeenBy(Myself),STATE_NOT_APPROACHABLE)
  WeaponEffectiveVs(FourthNearestEnemyOf(Myself),MAINHAND)
  WeaponCanDamage(FourthNearestEnemyOf(Myself),MAINHAND)
THEN
  RESPONSE #100
    AttackOneRound(FourthNearestEnemyOf(Myself))
END

IF
  ActionListEmpty()
  See(FifthNearestEnemyOf(Myself))
  !Allegiance(LastSeenBy(Myself),ENEMY)
  !StateCheck(LastSeenBy(Myself),STATE_NOT_APPROACHABLE)
  WeaponEffectiveVs(FifthNearestEnemyOf(Myself),MAINHAND)
  WeaponCanDamage(FifthNearestEnemyOf(Myself),MAINHAND)
THEN
  RESPONSE #100
    AttackOneRound(FifthNearestEnemyOf(Myself))
END

IF
  ActionListEmpty()
  See(SixthNearestEnemyOf(Myself))
  !Allegiance(LastSeenBy(Myself),ENEMY)
  !StateCheck(LastSeenBy(Myself),STATE_NOT_APPROACHABLE)
  WeaponEffectiveVs(SixthNearestEnemyOf(Myself),MAINHAND)
  WeaponCanDamage(SixthNearestEnemyOf(Myself),MAINHAND)
THEN
  RESPONSE #100
    AttackOneRound(SixthNearestEnemyOf(Myself))
END

/////////////////////////////////////////////////////////////////////
// Find them!
/////////////////////////////////////////////////////////////////////
IF
  Heard([ENEMY],125)
  !Detect([GOODCUTOFF])
THEN
  RESPONSE #100
    MoveToObject(LastHeardBy(Myself))
END
