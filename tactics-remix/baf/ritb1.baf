// Ritual Barbarian, other ritual thugs!
IF
    Global("Prep","LOCALS",0)
THEN
    RESPONSE #100
        SetGlobal("Prep","LOCALS",1)
        SetGlobalTimer("PrepTimer","LOCALS",0)
        SetGlobalTimer("KaiTimer","LOCALS",0)
END

IF
    GlobalTimerExpired("PrepTimer","LOCALS")
    Allegiance(Myself,ENEMY)
    !Gender(Myself,SUMMONED)
    !Race(Myself,GOLEM)
THEN
    RESPONSE #100
        SetGlobalTimer("PrepTimer","LOCALS",3600)
        SetGlobalTimer("Cast","LOCALS",0)
        GiveItemCreate("potn52",Myself,3,0,0)   // extra healing
        Continue()
END

// Target!
IF
    OR(8) // default ...
      See(Player1)
      See(Player2)
      See(Player3)
      See(Player4)
      See(Player5)
      See(Player6)
      See(WorstAC)
      See(LastAttackerOf(Myself))
    False()
THEN
    RESPONSE #100
END

IF
    CheckStat(SixthNearestEnemyOfType([GOODCUTOFF]),0,STONESKINS)
    !StateCheck(SixthNearestEnemyOfType([GOODCUTOFF]),STATE_IMPROVEDINVISIBILITY)
    CheckStat(SixthNearestEnemyOfType([GOODCUTOFF]),0,SANCTUARY)
    See(SixthNearestEnemyOfType([GOODCUTOFF]))
    False()
THEN
    RESPONSE #100
END

IF
    CheckStat(FifthNearestEnemyOfType([GOODCUTOFF]),0,STONESKINS)
    !StateCheck(FifthNearestEnemyOfType([GOODCUTOFF]),STATE_IMPROVEDINVISIBILITY)
    CheckStat(FifthNearestEnemyOfType([GOODCUTOFF]),0,SANCTUARY)
    See(FifthNearestEnemyOfType([GOODCUTOFF]))
    False()
THEN
    RESPONSE #100
END

IF
    CheckStat(FourthNearestEnemyOfType([GOODCUTOFF]),0,STONESKINS)
    !StateCheck(FourthNearestEnemyOfType([GOODCUTOFF]),STATE_IMPROVEDINVISIBILITY)
    CheckStat(FourthNearestEnemyOfType([GOODCUTOFF]),0,SANCTUARY)
    See(FourthNearestEnemyOfType([GOODCUTOFF]))
    False()
THEN
    RESPONSE #100
END

IF
    CheckStat(ThirdNearestEnemyOfType([GOODCUTOFF]),0,STONESKINS)
    !StateCheck(ThirdNearestEnemyOfType([GOODCUTOFF]),STATE_IMPROVEDINVISIBILITY)
    CheckStat(ThirdNearestEnemyOfType([GOODCUTOFF]),0,SANCTUARY)
    See(ThirdNearestEnemyOfType([GOODCUTOFF]))
    False()
THEN
    RESPONSE #100
END

IF
    CheckStat(SecondNearestEnemyOfType([GOODCUTOFF]),0,STONESKINS)
    !StateCheck(SecondNearestEnemyOfType([GOODCUTOFF]),STATE_IMPROVEDINVISIBILITY)
    CheckStat(SecondNearestEnemyOfType([GOODCUTOFF]),0,SANCTUARY)
    See(SecondNearestEnemyOfType([GOODCUTOFF]))
    False()
THEN
    RESPONSE #100
END

IF
    CheckStat(NearestEnemyOfType([GOODCUTOFF]),0,STONESKINS)
    !StateCheck(NearestEnemyOfType([GOODCUTOFF]),STATE_IMPROVEDINVISIBILITY)
    CheckStat(NearestEnemyOfType([GOODCUTOFF]),0,SANCTUARY)
    See(NearestEnemyOfType([GOODCUTOFF]))
    False()
THEN
    RESPONSE #100
END

IF
    CheckStat(WorstAC,0,STONESKINS)
    !StateCheck(WorstAC,STATE_IMPROVEDINVISIBILITY)
    CheckStat(WorstAC,0,SANCTUARY)
    See(WorstAC)
    False()
THEN
    RESPONSE #100
END

/////////////////////////////////////////////////////////////////////
// Heal, Minor
/////////////////////////////////////////////////////////////////////
IF
  GlobalTimerExpired("Cast","LOCALS")
  HasItem("potn52",Myself)
  HPPercentLT(Myself,90)
THEN
  RESPONSE #100
    SetGlobalTimer("Cast","LOCALS",6)
    UseItem("potn52",Myself)
END

IF
    Allegiance(Myself,ENEMY)
    GlobalTimerExpired("Cast","LOCALS")
    GlobalTimerExpired("KaiTimer","LOCALS")
    HaveSpell(BARBARIAN_RAGE)
    Range([PC],10)
THEN
    RESPONSE #100
        Spell(Myself,BARBARIAN_RAGE)
        SetGlobalTimer("KaiTimer","LOCALS",60)
        SetGlobalTimer("Cast","LOCALS",6)
END

IF
    Allegiance(Myself,ENEMY)
    GlobalTimerExpired("Cast","LOCALS")
    GlobalTimerExpired("KaiTimer","LOCALS")
    HaveSpell(MONK_STUNNING_BLOW)
    Range([PC],10)
THEN
    RESPONSE #100
        Spell(Myself,MONK_STUNNING_BLOW)
        SetGlobalTimer("KaiTimer","LOCALS",6)
        SetGlobalTimer("Cast","LOCALS",6)
END

IF
    Allegiance(Myself,ENEMY)
    !Allegiance(LastSeenBy(Myself),ENEMY)
THEN
    RESPONSE #100
        Attack(LastSeenBy(Myself))
END

/////////////////////////////////////////////////////////////////////
// Find them!
/////////////////////////////////////////////////////////////////////
IF
  Allegiance(Myself,ENEMY)
  Heard([ENEMY],125)
  !Detect([GOODCUTOFF])
THEN
  RESPONSE #100
    MoveToObject(LastHeardBy(Myself))
END
