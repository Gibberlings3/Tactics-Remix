// Selfish Joneleth
// Spells, Lower Magic Res, Spawn Liches

// Shatter Your Magic: inquis dispel, 50% spell failure, -50% MR
// teleports around

// takes the cloak of cheese

IF
  Global("Prep","LOCALS",0)
THEN
  RESPONSE #100
    SetGlobal("Prep","LOCALS",1)
    SetGlobalTimer("Spawn","LOCALS",18)
    SetGlobalTimer("MySpell","LOCALS",0)
    SetGlobalTimer("ProMag","LOCALS",24)
    ReallyForceSpell(Myself,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)
    ReallyForceSpell(Myself,WIZARD_SPELL_SHIELD)
    ReallyForceSpell(Myself,WIZARD_SPELL_TURNING)
    ReallyForceSpell(Myself,WIZARD_SPELL_IMMUNITY_ABJURATION)
END

IF
  !See(NearestEnemyOf(Myself))
  !HPPercent(Myself,100)
THEN
  RESPONSE #100
    SpellNoDec(Myself,CLERIC_CURE_CRITICAL_WOUNDS)
    Wait(2)
END

IF
  !See(NearestEnemyOf(Myself))
  HPPercent(Myself,100)
THEN
  RESPONSE #100
    MoveToObject(Player1)
END

IF
  !GlobalTimerNotExpired("ProMag","LOCALS")
THEN
  RESPONSE #100
    SpellNoDec(Myself,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)
    SetGlobalTimer("ProMag","LOCALS",24)
END

IF
  !GlobalTimerNotExpired("Spawn","LOCALS")
  !InMyArea("lich01")
  Difficulty(HARDEST)
THEN
  RESPONSE #100
    SetGlobalTimer("Spawn","LOCALS",30)
    CreateCreatureObjectOffScreen("lich01",Myself,0,0,0)
    Continue()
END

IF
  OR(7)
    See(Player1)
    See(Player2)
    See(Player3)
    See(Player4)
    See(Player5)
    See(Player6)
    See(NearestEnemyOf(Myself))
  False()
THEN
  RESPONSE #100
END

IF
  !GlobalTimerNotExpired("MySpell","LOCALS")
  !Allegiance(LastSeenBy(Myself),ENEMY)
THEN
  RESPONSE #100
    SetGlobalTimer("MySpell","LOCALS",18)
    ReallyForceSpellRES("ii0",LastSeenBy(Myself))
    Wait(2)
END

IF
  Range(NearestEnemyOf(Myself),10)
  Delay(6)
THEN
  RESPONSE #100
    ReallyForceSpellPoint([700.700], WIZARD_DIMENSION_DOOR)
  RESPONSE #100
    ReallyForceSpellPoint([800.1700], WIZARD_DIMENSION_DOOR)
  RESPONSE #100
    ReallyForceSpellPoint([1900.2100], WIZARD_DIMENSION_DOOR)
  RESPONSE #100
    ReallyForceSpellPoint([3000.1700], WIZARD_DIMENSION_DOOR)
  RESPONSE #100
    ReallyForceSpellPoint([3300.900], WIZARD_DIMENSION_DOOR)
  RESPONSE #100
    ReallyForceSpellPoint([2000.500], WIZARD_DIMENSION_DOOR)
  RESPONSE #100
    ReallyForceSpellPoint([1500.1000], WIZARD_DIMENSION_DOOR)
  RESPONSE #100
    ReallyForceSpellPoint([2500.1000], WIZARD_DIMENSION_DOOR)
  RESPONSE #100
    ReallyForceSpellPoint([1950.1300], WIZARD_DIMENSION_DOOR)
END

IF
  True()
  !Allegiance(LastSeenBy(Myself),ENEMY)
THEN
  RESPONSE #100
    SpellNoDec(LastSeenBy(Myself),WIZARD_ABI_DALZIMS_HORRID_WILTING)
    Wait(2)
  RESPONSE #100
    SpellNoDec(LastSeenBy(Myself),WIZARD_SYMBOL_STUN)
    Wait(2)
  RESPONSE #100
    SpellNoDec(LastSeenBy(Myself),WIZARD_SYMBOL_DEATH)
    Wait(2)
  RESPONSE #100
    SpellNoDec(LastSeenBy(Myself),WIZARD_FINGER_OF_DEATH)
    Wait(2)
  RESPONSE #100
    SpellNoDec(LastSeenBy(Myself),WIZARD_CHAIN_LIGHTNING)
    Wait(2)
  RESPONSE #100
    SpellNoDec(LastSeenBy(Myself),WIZARD_GREATER_MALISON)
    Wait(2)
  RESPONSE #100
    SpellNoDec(LastSeenBy(Myself),WIZARD_CHAOS)
    Wait(2)
  RESPONSE #100
    SpellNoDec(LastSeenBy(Myself),WIZARD_EMOTION_HOPELESSNESS)
    Wait(2)
  RESPONSE #100
    SpellNoDec(LastSeenBy(Myself),WIZARD_SLOW)
    Wait(2)
  RESPONSE #100
    SpellNoDec(LastSeenBy(Myself),WIZARD_MAGIC_MISSILE)
    Wait(2)
  RESPONSE #100
    SpellNoDec(LastSeenBy(Myself),WIZARD_MELF_ACID_ARROW)
    Wait(2)
  RESPONSE #100
    SpellNoDec(LastSeenBy(Myself),WIZARD_FLAME_ARROW)
    Wait(2)
  RESPONSE #100
    SpellNoDec(LastSeenBy(Myself),WIZARD_CONE_OF_COLD)
    Wait(2)
END
