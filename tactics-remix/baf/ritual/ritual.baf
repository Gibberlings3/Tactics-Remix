//////////////////////////////////////////////////////////////////////////
// These guys self-destruct at the end of the ritual.
//////////////////////////////////////////////////////////////////////////

IF
  GlobalTimerExpired("RitualIsNowOver","GLOBAL")
THEN
  RESPONSE #100
    DialogInterrupt(FALSE)
    SetInterrupt(FALSE)
    DisplayStringHead(Myself,@5008) // Strange transformation
    DestroyAllEquipment()
    CreateVisualEffectObject("SPPORTAL",Myself)
    Wait(2)
    Polymorph(SLIME_MUSTARD)
    Wait(3)
    Kill(Myself)
END

/////////////////////////////////////////////////////////////////////
// Should I become an enemy?
/////////////////////////////////////////////////////////////////////
IF
  !Allegiance(Myself,ENEMY)
  OR(5)
   AttackedBy([ANYTHING],DEFAULT)
   SpellCast([GOODCUTOFF],0)		// triggers on any spell
   SpellCastPriest([GOODCUTOFF],0)	// triggers on any spell
   SpellCastInnate([GOODCUTOFF],0)	// triggers on any ability
   Heard([ANYONE],99)
THEN
  RESPONSE #100
    Enemy()
    Shout(99)
END

/////////////////////////////////////////////////////////////////////
// Should I run away? 
/////////////////////////////////////////////////////////////////////
IF
  !Detect([GOODCUTOFF])
  Detect("ritskul1")
  OR(2)
   TookDamage()
   AttackedBy([ANYTHING],DEFAULT)
THEN
  RESPONSE #100
    DisplayStringHead(Myself,61849)
    MoveToObject("ritskul1")
END
