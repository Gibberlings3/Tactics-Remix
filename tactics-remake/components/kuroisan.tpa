// Tactics Remake, Kuroisan

///////////////////////////////////////////////////////////////////////////
// Import Files                                                          //
///////////////////////////////////////////////////////////////////////////
COPY ~tactics-remake/itm/solak1b.itm~ ~override~
COPY ~tactics-remake/itm/solak1.itm~  ~override~
        SAY NAME1 @4100
        SAY NAME2 @4102
        SAY UNIDENTIFIED_DESC @4101
        SAY DESC @4103

COPY ~tactics-remake/eff/morb03.eff~  ~override~
COPY ~tactics-remake/itm/morb03.itm~  ~override~
        SAY NAME1 @4200
        SAY NAME2 @4202
        SAY UNIDENTIFIED_DESC @4201
        SAY DESC @4203
        LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 177 target = 1 parameter1 = 0 parameter2 = 2 timing = 2 STR_VAR resource = ~morb03~ END

COPY ~tactics-remake/itm/mobt04.itm~  ~override~
        SAY NAME1 @4300
        SAY NAME2 @4302
        SAY UNIDENTIFIED_DESC @4301
        SAY DESC @4303

COPY ~tactics-remake/cre/solak1.cre~ ~override~
  SAY NAME1 @2003
  SAY NAME2 @2003
  REPLACE_CRE_ITEM ~morb03~ #0 #0 #0 ~UNSTEALABLE~ ~ARMOR~ EQUIP
  REPLACE_CRE_ITEM ~mobt04~ #0 #0 #0 ~UNSTEALABLE~ ~BOOTS~ EQUIP

COMPILE EVALUATE_BUFFER ~tactics-remake/dlg/solak1.d~

///////////////////////////////////////////////////////////////////////////
// Create and Add Spell State to Track Prior Target                      //
///////////////////////////////////////////////////////////////////////////
ACTION_IF !target_tracking BEGIN
  OUTER_SET target_tracking=1
  ACTION_IF (FILE_CONTAINS_EVALUATED (~splstate.ids~ ~PRIOR_TARGET~)) BEGIN
    COPY_EXISTING ~splstate.ids~ ~override~
    COUNT_2DA_COLS cols
    READ_2DA_ENTRIES_NOW rows cols
    FOR (row = 1; row < rows; ++row) BEGIN
      READ_2DA_ENTRY_FORMER rows row 1 ~state_name~
      READ_2DA_ENTRY_FORMER rows row 0 ~state_ind~
      PATCH_IF (~%state_name%~ STRING_EQUAL_CASE ~PRIOR_TARGET~) BEGIN
      SET pr_trgt = %state_ind%
      END
    END
    BUT_ONLY
  END

  ACTION_IF !(VARIABLE_IS_SET %pr_trgt%) BEGIN
    LAF ds_sort_ids STR_VAR ids=splstate END
    LAF d5_resolve_state STR_VAR new_state_id = ~PRIOR_TARGET~ RET new_state_ind END
    OUTER_SET pr_trgt = %new_state_ind%
  END

  COPY ~tactics-remake/spl/motrgt.spl~ ~override~
    LPF ADD_SPELL_EFFECT INT_VAR opcode = 328 target = 2 special = 1 parameter2 = %pr_trgt% timing = 0 duration = 6 END
END

///////////////////////////////////////////////////////////////////////////
// Update AI Scripts Based on Installed Mods                             //
///////////////////////////////////////////////////////////////////////////
ACTION_IF iwdification BEGIN
  INCLUDE ~tactics-remake/lib/iwd_dispelling_ak.tph~
END

///////////////////////////////////////////////////////////////////////////
// Copy .BCS file to override folder                                     //
///////////////////////////////////////////////////////////////////////////
EXTEND_TOP ~baldur.bcs~   ~tactics-remake/baf/solak1me.baf~
EXTEND_TOP ~baldur25.bcs~ ~tactics-remake/baf/solak1me.baf~

COPY ~tactics-remake/baf/solak1.baf~ ~override/solak1.bcs~
  COMPILE_BAF_TO_BCS

